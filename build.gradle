plugins {
    id 'java-library'
    id 'io.freefair.lombok' version "8.6"
    id 'io.qameta.allure' version '2.11.2'
}

allure {
    report {
        version.set("2.27.0")
    }
    adapter {
        aspectjVersion.set("1.9.22")
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.27.0")
            }
        }
    }
}

repositories {
    mavenCentral()
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            'io.qameta.allure:allure-selenide:2.27.0',
            'org.junit.jupiter:junit-jupiter:5.10.0',
            'io.rest-assured:rest-assured:5.3.1',
            'io.rest-assured:json-schema-validator:5.4.0',
            'io.qameta.allure:allure-rest-assured:2.27.0',
            'org.slf4j:slf4j-simple:2.0.7',
            'org.aeonbits.owner:owner:1.0.4',
            'org.assertj:assertj-core:3.25.3',
            'com.github.javafaker:javafaker:1.0.2',
            'commons-io:commons-io:2.11.0',
            'com.codeborne:selenide:7.4.0',
            "io.appium:java-client:8.4.0",
    )
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperties(System.getProperties())
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

task api(type: Test) {
    useJUnitPlatform {
        includeTags("api")
    }
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags("web")
    }
}

task mobile(type: Test) {
    useJUnitPlatform {
        includeTags("mobile")
    }
}


